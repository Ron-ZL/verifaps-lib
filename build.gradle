import java.text.SimpleDateFormat

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.70" apply false
    //id "com.github.hierynomus.license" version "0.14.0" apply false
    id "org.jetbrains.dokka" version "0.9.18"
    id "com.jfrog.bintray" version "1.8.4" apply false
    id "org.sonarqube" version "2.7.1"
    id "com.github.ben-manes.versions" version "0.21.0"
}

dokka {
    // List of files with module and package documentation
    // http://kotlinlang.org/docs/reference/kotlin-doc.html#module-and-package-documentation

    includes = ['README.md']

    outputFormat = "html"
    outputDirectory = "$buildDir/dokka"

    // List of files with module and package documentation
    // http://kotlinlang.org/docs/reference/kotlin-doc.html#module-and-package-documentation
    samples = []

    // These tasks will be used to determine source directories and classpath
    kotlinTasks {
        //defaultKotlinTasks() + subprojects*.compileKotlin
        project(":smv-model").compileKotlin
    }

    sourceDirs = files('smv-model/src/main/kotlin') +              \
                       files("iec61131lang/src/main/kotlin") +     \
                       files("iec-symbex/src/main/kotlin") +       \
                       files("iec-run/src/main/kotlin") +          \
                       files("iec-xml/src/main/kotlin") +          \
                       files("flycheck/src/main/kotlin") +         \
                       files("casestudies/src/main/kotlin") +      \
                       files("iec-modularization/src/main/kotlin")

    // Use default or set to custom path to cache directory
    // to enable package-list caching
    // When set to default, caches stored in $USER_HOME/.cache/dokka
    cacheRoot = 'default'

    // Use to include or exclude non public members.
    includeNonPublic = true

    // Do not output deprecated members. Applies globally, can be overridden by packageOptions
    skipDeprecated = false

    // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
    reportUndocumented = false

    skipEmptyPackages = false // Do not create index pages for empty packages

    impliedPlatforms = ["JVM"] // See platforms section of documentation

    jdkVersion = 11

    // No default documentation link to kotlin-stdlib
    noStdlibLink = false


    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/Verifaps/verifaps-lib/tree/master/"
        suffix = "#L"
    }
}


allprojects {
    apply plugin: 'idea'
    group = 'edu.kit.iti.formal.automation'
    version "0.10.0"

    idea {
        module {
            downloadJavadoc = false
            downloadSources = true
        }
    }


    repositories{
        mavenCentral()
        jcenter()
    }

    sonarqube {
        properties {
            property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
            property "sonar.exclusions", "*/build/generated-src/antlr/**"
            property "sonar.c.file.suffixes", "-"
            property "sonar.cpp.file.suffixes", "-"
            property "sonar.objc.file.suffixes", "-"
        }
    }
}


subprojects {
    apply plugin: 'maven-publish'
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'jacoco'

    ext.kotlin_version = '1.3.61'

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            suppressWarnings = true
            jvmTarget = "11"
        }
    }

    test {
        useJUnitPlatform {
            //excludeTag 'edu.kit.iti.formal.automation.tests.CategoryNonCI'
        }

        jacoco {

        }
    }

    jacoco {
        toolVersion = "0.8.4"
    }


    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
        }
    }

    task testJar(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
        from sourceSets.test.resources
    }

    compileKotlin {
        kotlinOptions {
            suppressWarnings = true
            jvmTarget = "1.8"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenLocal()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://jcenter.bintray.com" }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        compile group: 'org.jetbrains', name: 'annotations', version: '17.0.0'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
        compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'
        compile group: 'io.github.microutils', name: 'kotlin-logging', version: '1.7.5'

        testCompile 'com.google.truth:truth:1.0'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.5.1'

        testCompile project(':utils-test')

        //testCompile 'org.jetbrains.spek:spek-api:1.2.1'
        //testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.2.1'
        //compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.20'
    }

    test {
        useJUnitPlatform()
    }

    task versionInfo(type: WriteProperties) {
            outputFile = new java.io.File(sourceSets.main.resources.srcDirs.first(), "META-INF/version.property")
            def gitversion = "1"//"git describe --tags".execute()
            //gitversion.waitFor()
            def gv = "1.0" //gitversion.in.readLines().get(0)

            setProperties([
                    "project-name"   : project.name ?: "",
                    "project-version": project.version ?: "0",
                    "description"    : description ?: "",
                    'built-by'       : System.properties['user.name'],
                    'build-timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                    'build-revision' : gv,
                    'created-by'     : "Gradle ${gradle.gradleVersion}",
                    'build-jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'build-os'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            ])
    }

    processResources.dependsOn << versionInfo

    jar {
        manifest {
            attributes(
                    "Implementation-Title": project.name,
                    "Implementation-Version": version ?: "0",
                    "Description": description,
                    'Built-By': System.properties['user.name'],
                    'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                    'Created-By': "Gradle ${gradle.gradleVersion}",
                    'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                    'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
            )
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/verifaps/verifaps-lib")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    name = project.name
                    description = project.description
                    url = 'https://formal.iti.kit.edu/verifaps'
                    licenses {
                        license {
                            name = 'Gnu Public License, Version 3.0'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                        }
                    }
                    developers {
                        developer {
                            id = 'weigl'
                            name = 'Alexander Weigl'
                            email = 'weigl@kit.edu'
                        }
                    }
                    scm {
                        connection = 'https://github.com/verifaps/verifaps-lib.git'
                        url = 'https://github.com/verifaps/verifaps-lib.git'
                    }
                }
            }
        }
    }

    javadoc {
        failOnError = false
        verbose = false
    }

    /*
    license {
        mapping("java", "PHP")
        mapping("kt", "PHP")

        header = file("gradle/HEADER")
        ext.name = "geteta"
        ext.holder = "Alexander Weigl <weigl@kit.edu>"
        ext.year = "2016-2018"
        exclude "*.xml"
        exclude "*.g4"
        exclude "*.smv"
    }
    */
    //Checks does not work
    //licenseMain.enabled = false
    //licenseTest.enabled = false

    //sourceSets { test { resources { srcDirs "../share/test-resources" } } }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

